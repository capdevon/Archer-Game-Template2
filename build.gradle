plugins {
    id 'application'
    id 'java'
}

description = 'Archer Game Template'

// select one source-code (JDK) option
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// select one version of the Engine:
ext.jmonkeyengineVersion = '3.4.0-stable' // from mavenCentral

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // runtime options:
        args = []
        classpath sourceSets.main.runtimeClasspath
        //debug true
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xbatch'
        //jvmArgs '-Xms512m', '-Xmx512m'
        //jvmArgs '-XX:+PrintCompilation'
        //jvmArgs '-XX:+UseConcMarkSweepGC'
        jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
mainClassName = 'mygame.Main'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal()
    mavenCentral()
    //maven { url 'http://nifty-gui.sourceforge.net/nifty-maven-repo' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://jcenter.bintray.com/' }

    // Read more about repositories here:
    //   https://docs.gradle.org/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // You can read more about how to add dependencies here:
    //   https://docs.gradle.org/current/userguide/dependency_management.html#sec:how_to_declare_your_dependencies

    // Engine
    implementation 'org.jmonkeyengine:jme3-core:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-effects:' + jmonkeyengineVersion
    //implementation 'org.jmonkeyengine:jme3-networking:' + jmonkeyengineVersion
    //implementation 'org.jmonkeyengine:jme3-niftygui:' + jmonkeyengineVersion
    //implementation 'org.jmonkeyengine:jme3-terrain:' + jmonkeyengineVersion

    // Minie
    implementation 'com.github.stephengold:Minie:4.3.0'

    // select one version of LWJGL (from mavenCentral or mavenLocal)
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl:' + jmonkeyengineVersion  // LWJGL 2.x
    //runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmonkeyengineVersion // LWJGL 3.x

    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-testdata:' + jmonkeyengineVersion
    
    // Libraries related to the Lemur GUI and Groovy:
    //implementation 'com.simsilica:lemur:1.15.0'
    //implementation 'com.simsilica:lemur-props:1.1.1'
    //implementation 'com.simsilica:lemur-proto:1.12.0'
    //runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.7'
    
    // Additional filters
    implementation 'com.github.polincdev:ShaderBlowEx:-SNAPSHOT'
    
    runtimeOnly files('lib/jme-materialize.jar')
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
